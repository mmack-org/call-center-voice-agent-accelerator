param location string
param environmentName string
param uniqueSuffix string
param tags object
param exists bool
param identityId string
param identityClientId string
param fabricReadIdentityId string
param fabricReadIdentityClientId string
param fabricWriteIdentityId string
param fabricWriteIdentityClientId string
param authorizedCustomerKey string = ''
param containerRegistryName string
param aiServicesEndpoint string
param modelDeploymentName string
param voiceName string = 'fr-FR-DeniseNeural'
param enableFoundryAgent bool = false
param foundryProjectName string = ''
param foundryAgentName string = ''
param acsConnectionStringSecretUri string
param twilioAuthTokenSecretUri string = ''
param infobipApiKeySecretUri string = ''
param infobipApiBaseUrl string = ''
param genesysApiKeySecretUri string = ''
param sinchApplicationKeySecretUri string = ''
param sinchApplicationSecretSecretUri string = ''
param bandwidthClientIdSecretUri string = ''
param bandwidthClientSecretSecretUri string = ''
param bandwidthAccountId string = ''
param bandwidthApplicationId string = ''
param logAnalyticsWorkspaceName string
param appInsightsConnectionString string = ''
@description('The name of the container image')
param imageName string = ''
param debugMode bool = false
@description('Enable zone redundancy for the Container App Environment')
param zoneRedundant bool = true

// Helper to sanitize environmentName for valid container app name
var sanitizedEnvName = toLower(replace(replace(replace(environmentName, ' ', '-'), '--', '-'), '_', '-'))
var containerAppName = take('ca-${sanitizedEnvName}-${uniqueSuffix}', 32)
var containerEnvName = take('cae-${sanitizedEnvName}-${uniqueSuffix}', 32)

resource logAnalyticsWorkspace 'Microsoft.OperationalInsights/workspaces@2022-10-01' existing = { name: logAnalyticsWorkspaceName }


module fetchLatestImage './fetch-container-image.bicep' = {
  name: '${containerAppName}-fetch-image'
  params: {
    exists: exists
    name: containerAppName
  }
}

resource containerAppEnv 'Microsoft.App/managedEnvironments@2023-05-01' = {
  name: containerEnvName
  location: location
  tags: tags
  properties: {
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: logAnalyticsWorkspace.properties.customerId
        sharedKey: logAnalyticsWorkspace.listKeys().primarySharedKey
      }
    }
  }
}

resource containerApp 'Microsoft.App/containerApps@2024-10-02-preview' = {
  name: containerAppName
  location: location
  tags: union(tags, { 'azd-service-name': 'app' })
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${identityId}': {}
      '${fabricReadIdentityId}': {}
      '${fabricWriteIdentityId}': {}
    }
  }
  properties: {
    managedEnvironmentId: containerAppEnv.id
    configuration: {
      activeRevisionsMode: 'Single'
      ingress: {
        external: true
        targetPort: 8000
        transport: 'auto'
      }
      registries: [
        {
          server: '${containerRegistryName}.azurecr.io'
          identity: identityId
        }
      ]
      secrets: concat(
        !empty(acsConnectionStringSecretUri) ? [
          {
            name: 'acs-connection-string'
            keyVaultUrl: acsConnectionStringSecretUri
            identity: identityId
          }
        ] : [],
        !empty(twilioAuthTokenSecretUri) ? [
          {
            name: 'twilio-auth-token'
            keyVaultUrl: twilioAuthTokenSecretUri
          identity: identityId
        }
      ] : [],
        !empty(infobipApiKeySecretUri) ? [
          {
            name: 'infobip-api-key'
            keyVaultUrl: infobipApiKeySecretUri
            identity: identityId
          }
        ] : [],
        !empty(genesysApiKeySecretUri) ? [
          {
            name: 'genesys-api-key'
            keyVaultUrl: genesysApiKeySecretUri
            identity: identityId
          }
        ] : [],
        !empty(sinchApplicationKeySecretUri) ? [
          {
            name: 'sinch-application-key'
            keyVaultUrl: sinchApplicationKeySecretUri
            identity: identityId
          }
        ] : [],
        !empty(sinchApplicationSecretSecretUri) ? [
          {
            name: 'sinch-application-secret'
            keyVaultUrl: sinchApplicationSecretSecretUri
            identity: identityId
          }
        ] : [],
        !empty(bandwidthClientIdSecretUri) ? [
          {
            name: 'bandwidth-client-id'
            keyVaultUrl: bandwidthClientIdSecretUri
            identity: identityId
          }
        ] : [],
        !empty(bandwidthClientSecretSecretUri) ? [
          {
            name: 'bandwidth-client-secret'
            keyVaultUrl: bandwidthClientSecretSecretUri
            identity: identityId
          }
        ] : [])
    }
    template: {
      containers: [
        {
          name: 'main'
          image: !empty(imageName) ? imageName : 'mcr.microsoft.com/azuredocs/containerapps-helloworld:latest'
          env: concat([
            {
              name: 'AZURE_VOICE_LIVE_ENDPOINT'
              value: aiServicesEndpoint
            }
            {
              name: 'AZURE_USER_ASSIGNED_IDENTITY_CLIENT_ID'
              value: identityClientId
            }
            {
              name: 'FABRIC_READ_IDENTITY_CLIENT_ID'
              value: fabricReadIdentityClientId
            }
            {
              name: 'FABRIC_WRITE_IDENTITY_CLIENT_ID'
              value: fabricWriteIdentityClientId
            }
            {
              name: 'AUTHORIZED_CUSTOMER_KEY'
              value: authorizedCustomerKey
            }
            {
              name: 'VOICE_LIVE_MODEL'
              value: modelDeploymentName
            }
            {
              name: 'VOICE_LIVE_VOICE'
              value: voiceName
            }
            {
              name: 'ENABLE_FOUNDRY_IQ'
              value: string(enableFoundryAgent)
            }
            {
              name: 'AZURE_AI_FOUNDRY_PROJECT_NAME'
              value: foundryProjectName
            }
            {
              name: 'AZURE_AI_FOUNDRY_AGENT_ID'
              value: foundryAgentName
            }
            {
              name: 'DEBUG_MODE'
              value: string(debugMode)
            }
            {
              name: 'APPLICATIONINSIGHTS_CONNECTION_STRING'
              value: appInsightsConnectionString
            }
          ], !empty(acsConnectionStringSecretUri) ? [
            {
              name: 'ACS_CONNECTION_STRING'
              secretRef: 'acs-connection-string'
            }
          ] : [], !empty(twilioAuthTokenSecretUri) ? [
            {
              name: 'TWILIO_AUTH_TOKEN'
              secretRef: 'twilio-auth-token'
            }
          ] : [], !empty(infobipApiKeySecretUri) ? [
            {
              name: 'INFOBIP_API_KEY'
              secretRef: 'infobip-api-key'
            }
            {
              name: 'INFOBIP_API_BASE_URL'
              value: infobipApiBaseUrl
            }
          ] : [], !empty(genesysApiKeySecretUri) ? [
            {
              name: 'GENESYS_API_KEY'
              secretRef: 'genesys-api-key'
            }
          ] : [], !empty(sinchApplicationKeySecretUri) ? [
            {
              name: 'SINCH_APPLICATION_KEY'
              secretRef: 'sinch-application-key'
            }
          ] : [], !empty(sinchApplicationSecretSecretUri) ? [
            {
              name: 'SINCH_APPLICATION_SECRET'
              secretRef: 'sinch-application-secret'
            }
          ] : [], !empty(bandwidthClientIdSecretUri) ? [
            {
              name: 'BANDWIDTH_CLIENT_ID'
              secretRef: 'bandwidth-client-id'
            }
            {
              name: 'BANDWIDTH_ACCOUNT_ID'
              value: bandwidthAccountId
            }
            {
              name: 'BANDWIDTH_APPLICATION_ID'
              value: bandwidthApplicationId
            }
          ] : [], !empty(bandwidthClientSecretSecretUri) ? [
            {
              name: 'BANDWIDTH_CLIENT_SECRET'
              secretRef: 'bandwidth-client-secret'
            }
          ] : [])
          resources: {
            cpu: json('2.0')
            memory: '4.0Gi'
          }
        }
      ]
      // TODO add memory/cpu scaling
      scale: {
        minReplicas: 1
        maxReplicas: 10
        rules: [
          {
            name: 'http-scaler'
            http: {
              metadata: {
                concurrentRequests: '100'
              }
            }
          }
        ]
      }
    }
  }
}

output containerAppFqdn string = containerApp.properties.configuration.ingress.fqdn
output containerAppId string = containerApp.id
output containerAppName string = containerApp.name
